options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(FriendTJMem)
  public class FriendTJMem {}
PARSER_END(FriendTJMem)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

TOKEN : 
{
  < REGLIMIT: "/*" <INTEGER_LITERAL> "*/" >
| <TOP_IMPORT: "import static a5.Memory.*;">
| <ALLOCA: "alloca(" <INTEGER_LITERAL> ");">
| <REGISTER_OBJECT: "Object">
| <MEM_LOAD: "load">
| <FLOAT_TYPE: "float">
| <INT_TYPE: "int">
| <BOOLEAN_TYPE: "boolean">
| <MEM_STORE: "store">
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN : 
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < DOT: "." >
| < ASSIGN: "=" >
| < LT: "<" >
| < PLUS: "+" >
| < MINUS: "-" >
| < AND : "&" >   
| < NOT : "!" >   
| < CLASS: "class" >
| < INTERFACE: "interface" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < FOR: "for" >
| < LENGTH: "length" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < SCOMMENT1: "/*" >
| < SCOMMENT2: "*/" >
| < LIVENESSQUERY: "PRINTLIVEVARIABLES" >
| < VOID: "void" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
| < FLOAT_LITERAL: <INTEGER_LITERAL> <DOT> ( (["0"-"9"])* ["1"-"9"] ) >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/***********************************
 * The PoohTJ Grammar Starts Here *
 ***********************************/


void Goal() :
{}
{
  <REGLIMIT>
  <TOP_IMPORT>
  MainClass()
  ( TypeDeclaration() )*
  <EOF>
}

void MainClass() :
{}
{
  "class" Identifier() "{"
    "public" "static" "void" "main" "(" "String" "[" "]" Identifier() ")"   
    "{" 
      ( LOOKAHEAD(VarDeclaration()) VarDeclaration() )*
      <ALLOCA>
      ( Statement() )*
     "}"
  "}"
}

void TypeDeclaration() :  
{}
{ 
  LOOKAHEAD(3)
  ClassDeclaration()
|
  ClassExtendsDeclaration()
}

void ClassDeclaration() :
{}
{
  "class" Identifier() "{" ( VarDeclaration() )* 
                           ( MethodDeclaration() )* 
                       "}"
}

void ClassExtendsDeclaration() :
{}
{
  "class" Identifier() "extends" Identifier()
                       "{" ( VarDeclaration() )* 
                           ( MethodDeclaration() )* 
                       "}"
}

void VarDeclaration() :  
{}
{ 
  <REGISTER_OBJECT> Identifier() ";"      
}

void MethodDeclaration() :  
{}
{
  "public" Type() Identifier() 
  "(" ( FormalParameterList() )? ")"   
  "{" ( LOOKAHEAD(2) VarDeclaration() )* 
      <ALLOCA>
      ( Statement() )* "return" 
      
      (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad())
      
      ";" "}"
}

void FormalParameterList() :
{}
{
  FormalParameter() ( FormalParameterRest() )*
}

void FormalParameter() :
{}
{
  Type() Identifier()
}

void FormalParameterRest() :
{}
{
  "," FormalParameter()
}

void RegisterLoad() :
{}
{
  "(" "(" Type() ")" Identifier() ")" 
}

void MemoryLoad() :
{}
{
  "(" "(" Type() ")" <MEM_LOAD> "(" <INTEGER_LITERAL>  ")" ")" 
}

void MemoryStoreStatement() :
{}
{
  "store" "(" <INTEGER_LITERAL> "," Expression() ")" ";"
}

void Type() :
{}
{
  LOOKAHEAD(3)
  ArrayType()
|
  BooleanType()
|
  IntegerType()
|
  FloatType()
|
  Identifier()     
}

void ArrayType() :
{}
{
  <INT_TYPE> "[" "]"
}

void FloatType() :
{}
{
  <FLOAT_TYPE>
}

void BooleanType() :
{}
{
  <BOOLEAN_TYPE>
}

void IntegerType() :
{}
{
  <INT_TYPE>
}

void Statement() :
{}
{
  Block()
| 
  LOOKAHEAD(2)
  MemoryStoreStatement()
|
  LOOKAHEAD(2)
  AssignmentStatement()
|
  LOOKAHEAD(2)
  ArrayAssignmentStatement()
|
  LOOKAHEAD(2)
  FieldAssignmentStatement()
|
  IfStatement()
|
  WhileStatement()
|
  PrintStatement()
|
  LivenessQueryStatement()
}

void Block() :
{}
{
  "{" ( Statement() )* "}"
}

void AssignmentStatement() :
{}
{
  Identifier() "=" Expression() ";"
}

void ArrayAssignmentStatement() :
{}
{
  Identifier() "[" Identifier() "]" "=" Identifier() ";"
}

void FieldAssignmentStatement() :
{}
{
  Identifier() "." Identifier()  "=" Identifier() ";"
}

void IfStatement() :
{}
{
  LOOKAHEAD(IfthenElseStatement()) IfthenElseStatement()
  | IfthenStatement()
}
void IfthenStatement() :
{}
{
  "if" "(" 
  (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad())
  ")" Statement()
}
void IfthenElseStatement() :
{}
{
  "if" "("
  (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad()) 
  ")" Statement() "else" Statement()
}

void WhileStatement() :
{}
{
  "while" "(" 
  (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad())
  ")" Statement() 
}

void PrintStatement() :
{}
{
  "System.out.println" "(" 
  (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad())
  ")" ";"
}

void LivenessQueryStatement() :
{}
{
  <SCOMMENT1> <LIVENESSQUERY> <SCOMMENT2>
}

void Expression() :
{}
{
  LOOKAHEAD(OrExpression())
  OrExpression()
|
  LOOKAHEAD(AndExpression())
  AndExpression()
|
  LOOKAHEAD(CompareExpression())
  CompareExpression()
|
  LOOKAHEAD(neqExpression())
  neqExpression()
|
  LOOKAHEAD(PlusExpression())
  PlusExpression()
|
  LOOKAHEAD(MinusExpression())
  MinusExpression()
|
  LOOKAHEAD(TimesExpression())
  TimesExpression()
|
  LOOKAHEAD(DivExpression())
  DivExpression()
|
  LOOKAHEAD(ArrayLookup())
  ArrayLookup()
|
  LOOKAHEAD(ArrayLength())
  ArrayLength()
|
  LOOKAHEAD(MessageSend())
  MessageSend()
| 
  PrimaryExpression()
}

void AndExpression() :
{} 
{
  PrimaryExpression() 
  "&&"
  PrimaryExpression()
}

void OrExpression() :
{} 
{
  PrimaryExpression()
  "||"
  PrimaryExpression()
}

void CompareExpression() :
{} 
{
  PrimaryExpression()
  "<="
  PrimaryExpression()
}

void neqExpression() :
{} 
{
  PrimaryExpression()
  "!="
  PrimaryExpression()
}

void PlusExpression() :
{}
{
  PrimaryExpression()
  "+"
  PrimaryExpression()
}

void MinusExpression() :
{}
{
  PrimaryExpression()
  "-"
  PrimaryExpression()
}

void TimesExpression() :
{}
{
  PrimaryExpression()
  "*" 
  PrimaryExpression()
}

void DivExpression() :
{}
{
  PrimaryExpression()
  "/" 
  PrimaryExpression()
}

void ArrayLookup() :
{}
{
  Identifier() "[" Identifier() "]"
}

void ArrayLength() :
{}
{
  Identifier() "." "length"
}

void MessageSend() :
{}
{
  
  (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad()) 
  "." 
  Identifier() 
  "(" ( ArgList() )? ")"
}

void ArgList() :
{}
{
  (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad()) ( ArgRest() )*
}

void ArgRest() :
{}
{
  "," (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad())
}

void PrimaryExpression() :
{}
{
  LOOKAHEAD(RegisterLoad())
  RegisterLoad()
|
  LOOKAHEAD(MemoryLoad())
  MemoryLoad()
|
  IntegerLiteral()
|
  FloatLiteral()
|
  TrueLiteral()
|
  FalseLiteral()
|
  Identifier()
|
  ThisExpression()
|
  LOOKAHEAD(3)
  ArrayAllocationExpression()
|
  AllocationExpression()
|   
  NotExpression()
}

void IntegerLiteral() :
{}
{
  <INTEGER_LITERAL>
}

void FloatLiteral() :
{}
{
  <FLOAT_LITERAL>
}


void TrueLiteral() :
{}
{
  "true"
}

void FalseLiteral() :
{}
{
  "false"
}

void Identifier() :
{}
{
  <IDENTIFIER>
}

void ThisExpression() :
{}
{
  "this"
}

void ArrayAllocationExpression() :
{}
{
  "new" "int" "[" Identifier() "]"
}

void AllocationExpression() :
{}
{
  "new" Identifier() "(" ")"
}

void NotExpression() :
{}
{
  "!" (LOOKAHEAD(Identifier()) Identifier() | LOOKAHEAD(RegisterLoad()) RegisterLoad() | LOOKAHEAD(MemoryLoad()) MemoryLoad())
}

